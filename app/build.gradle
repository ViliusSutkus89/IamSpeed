plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin' version '2.5.0'
    id 'kotlin-kapt'
    id "com.mikepenz.aboutlibraries.plugin" version "8.9.4"
}

android {
    compileSdk 32
    defaultConfig {
        applicationId "com.viliussutkus89.iamspeed"
        minSdk 16
        targetSdk 32

        versionCode 151
        versionName "1.1.1"

        vectorDrawables.useSupportLibrary true

        multiDexEnabled true

        if (project.hasProperty('test_build')) {
            versionNameSuffix = ".unreleased"
        }
    }
    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix = ".debug"
            applicationIdSuffix = ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    lint.abortOnError = false

    if (System.getenv('SIGNING_KEYFILE') && System.getenv('SIGNING_ALIAS') && System.getenv('SIGNING_PASS')) {
        signingConfigs {
            release {
                storeFile file(System.getenv('SIGNING_KEYFILE'))
                storePassword System.getenv('SIGNING_PASS')
                keyAlias System.getenv('SIGNING_ALIAS')
                keyPassword System.getenv('SIGNING_PASS')
            }
        }
        buildTypes.release.signingConfig signingConfigs.release
    }
}

dependencies {
    implementation 'androidx.core:core:1.8.0'
    implementation 'androidx.core:core-ktx:1.8.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'androidx.appcompat:appcompat:1.5.1'

    implementation 'com.google.android.material:material:1.6.1'

    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-service:2.5.1'

    implementation 'androidx.preference:preference:1.2.0'

    implementation "androidx.navigation:navigation-fragment-ktx:2.5.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.2"

    // AboutLibraries-v10 requires AndroidSDK-21
    implementation "com.mikepenz:aboutlibraries:8.9.4"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
}
